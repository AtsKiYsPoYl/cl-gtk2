(defpackage :gtk-demo
  (:use :cl :gtk :gdk :gobject :anaphora :iter)
  (:export #:test
           #:test-entry
           #:table-packing
           #:test-pixbuf
           #:test-image
           #:test-progress-bar
           #:test-status-bar
           #:test-scale-button
           #:test-text-view
           #:demo-code-editor
           #:test-treeview-list
           #:test-combobox
           #:test-toolbar
           #:test-color-button
           #:test-ui-manager))

(in-package :gtk-demo)

(defun test ()
  (let ((window (make-instance 'gtk-window :type :toplevel :app-paintable t))
        x y)
    (g-signal-connect window "destroy" (lambda (widget)
                                         (release widget)
                                         (gtk-main-quit)))
    (g-signal-connect window "motion-notify-event" (lambda (widget event)
                                                     (release widget)
                                                     (setf x (event-motion-x event)
                                                           y (event-motion-y event))
                                                     (gtk-widget-queue-draw window)))
    (g-signal-connect window "expose-event"
                      (lambda (widget event)
                        (declare (ignore event))
                        (release widget)
                        ;(print event)
                        (using* ((gdk-window (widget-window window))
                                 (gc (gdk-gc-new gdk-window))
                                 (layout (gtk-widget-create-pango-layout window (format nil "X: ~F~%Y: ~F" x y))))
                          (gdk-draw-layout gdk-window gc 0 0 layout)
                          (gdk-gc-set-rgb-fg-color gc (make-color :red 65535 :green 0 :blue 0))
                          (multiple-value-bind (x y) (drawable-get-size gdk-window)
                            (gdk-draw-line gdk-window gc 0 0 x y)))))
    (g-signal-connect window "configure-event"
                      (lambda (widget event)
                        (declare (ignore event))
                        (release widget)
                        (gtk-widget-queue-draw window)))
    (gtk-widget-show-all window)
    (push :pointer-motion-mask (gdk-window-events (widget-window window)))
    (gtk-main)
    (release window)))

(defun test-entry ()
  (using* ((window (make-instance 'gtk-window :type :toplevel :title "Testing entry" :border-width 10))
           (box (make-instance 'v-box))
           (entry (make-instance 'entry))
           (button (make-instance 'button :label "OK"))
           (text-buffer (make-instance 'text-buffer))
           (text-view (make-instance 'text-view :buffer text-buffer))
           (button-select (make-instance 'button :label "Select"))
           (button-insert (make-instance 'button :label "Insert")))
    (box-pack-start box (make-instance 'label :label "Enter <b>anything</b> you wish:" :use-markup t) :expand nil)
    (box-pack-start box entry :expand nil)
    (box-pack-start box button :expand nil)
    (box-pack-start box button-select :expand nil)
    (box-pack-start box button-insert :expand nil)
    (using* ((w (make-instance 'scrolled-window)))
      (box-pack-start box w)
      (container-add w text-view))
    (container-add window box)
    (g-signal-connect window "destroy" (lambda (widget) (release widget) (gtk-main-quit)))
    (g-signal-connect window "delete-event" (lambda (widget event)
                                              (declare (ignore event))
                                              (release widget)
                                              (using (dlg (make-instance 'message-dialog :text "Are you sure?" :buttons :yes-no))
                                                (let ((response (dialog-run dlg)))
                                                  (object-destroy dlg)
                                                  (not (eq :yes response))))))
    (g-signal-connect button "clicked" (lambda (button) (release button)
                                               (setf (text-buffer-text text-buffer)
                                                     (format nil "~A~%~A" (text-buffer-text text-buffer) (entry-text entry))
                                                     (entry-text entry) "")))
    (g-signal-connect button-select "clicked" (lambda (button) (release button)
                                               (editable-select-region entry 5 10)))
    (g-signal-connect button-insert "clicked" (lambda (button) (release button)
                                                      (editable-insert-text entry "hello" 2)))
    (gtk-widget-show-all window)
    (gtk-main)))

(defun table-packing ()
  (using* ((window (make-instance 'gtk-window :type :toplevel :title "Table packing" :border-width 20))
           (table (make-instance 'table :n-rows 2 :n-columns 2 :homogeneous t))
           (button-1 (make-instance 'button :label "Button 1"))
           (button-2 (make-instance 'button :label "Button 2"))
           (button-q (make-instance 'button :label "Quit")))
    (container-add window table)
    (table-attach table button-1 0 1 0 1)
    (table-attach table button-2 1 2 0 1)
    (table-attach table button-q 0 2 1 2)
    (g-signal-connect window "destroy" (lambda (w) (release w) (gtk-main-quit)))
    (g-signal-connect button-q "clicked" (lambda (b) (release b) (object-destroy window)))
    (gtk-widget-show-all window)
    (gtk-main)))

(defun test-pixbuf ()
  (using* ((window (make-instance 'gtk-window :title "Test pixbuf" :request-width 600 :request-height 240))
           (vbox (make-instance 'v-box))
           (eventbox (make-instance 'event-box))
           (vbox-1 (make-instance 'v-box)))
    (container-add window vbox)
    (box-pack-start vbox (make-instance 'label :text "Placing bg image" :font "Times New Roman Italic 10" :color "#00f" :request-height 40))
    (g-signal-connect window "destroy" (lambda (w) (release w) (gtk-main-quit)))
    (box-pack-start vbox eventbox)
    (container-add eventbox vbox-1)
    (box-pack-start vbox-1 (make-instance 'label :text "This is the eventbox"))
    (box-pack-start vbox-1 (make-instance 'label :text "The green ball is the bg"))
    (gtk-widget-show-all window)
    (gtk-main)))

(defun test-image ()
  (using*((window (make-instance 'gtk-window :title "Test images"))
          (image (make-instance 'image :icon-name "applications-development" :icon-size 6)))
    (container-add window image)
    (g-signal-connect window "destroy" (lambda (w) (release w) (gtk-main-quit)))
    (gtk-widget-show-all window)
    (gtk-main)))

(defun test-progress-bar ()
  (using* ((window (make-instance 'gtk-window :title "Test progress bar"))
           (v-box (make-instance 'v-box))
           (p-bar (make-instance 'progress-bar :test "A process"))
           (button-pulse (make-instance 'button :label "Pulse"))
           (button-set (make-instance 'button :label "Set"))
           (entry (make-instance 'entry)))
    (g-signal-connect window "destroy" (lambda (w) (release w) (gtk-main-quit)))
    (container-add window v-box)
    (box-pack-start v-box p-bar)
    (box-pack-start v-box button-pulse)
    (box-pack-start v-box button-set)
    (box-pack-start v-box entry)
    (g-signal-connect button-pulse "clicked" (lambda (w) (release w) (progress-bar-pulse p-bar)))
    (g-signal-connect button-set "clicked" (lambda (w) (release w)
                                                   (setf (progress-bar-fraction p-bar)
                                                         (coerce (read-from-string (entry-text entry)) 'real))))
    (gtk-widget-show-all window)
    (gtk-main)))

(defun test-status-bar ()
  (using* ((window (make-instance 'gtk-window :title "Text status bar"))
           (v-box (make-instance 'v-box))
           (h-box (make-instance 'h-box))
           (label (make-instance 'label :label "Test of status bar" :xalign 0.5 :yalign 0.5))
           (status-bar (make-instance 'statusbar :has-resize-grip t))
           (button-push (make-instance 'button :label "Push"))
           (button-pop (make-instance 'button :label "Pop"))
           (entry (make-instance 'entry))
           (icon (make-instance 'status-icon :icon-name "applications-development")))
    (set-status-icon-tooltip icon "An icon from lisp program")
    (g-signal-connect window "destroy" (lambda (w) (release w)
                                               #+ (or) (setf (status-icon-visible icon) nil)
                                               (gtk-main-quit)))
    (g-signal-connect button-push "clicked" (lambda (b) (release b) (status-bar-push status-bar "lisp-prog" (entry-text entry))))
    (g-signal-connect button-pop "clicked" (lambda (b) (release b) (status-bar-pop status-bar "lisp-prog")))
    (g-signal-connect icon "activate" (lambda (i) (release i)
                                              (using (message-dialog (make-instance 'message-dialog :buttons :ok :text "You clicked on icon!"))
                                                (dialog-run message-dialog)
                                                (object-destroy message-dialog))))
    (container-add window v-box)
    (box-pack-start v-box h-box :expand nil)
    (box-pack-start h-box entry)
    (box-pack-start h-box button-push :expand nil)
    (box-pack-start h-box button-pop :expand nil)
    (box-pack-start v-box label)
    (box-pack-start v-box status-bar :expand nil)
    (gtk-widget-show-all window)
    (setf (status-icon-screen icon) (gtk-window-screen window))
    (gtk-main)))

(defun test-scale-button ()
  (using* ((window (make-instance 'gtk-window :type :toplevel :title "Testing scale button"))
           (button (make-instance 'scale-button :icons (list "media-seek-backward" "media-seek-forward" "media-playback-stop" "media-playback-start") :adjustment (make-instance 'adjustment :lower -40 :upper 50 :value 20))))
    (g-signal-connect window "destroy" (lambda (w) (release w) (gtk-main-quit)))
    (container-add window button)
    (gtk-widget-show-all window)
    (gtk-main)))

(defun test-text-view ()
  (using* ((window (make-instance 'gtk-window :type :toplevel :title "Testing text view" :width-request 400 :height-request 300))
           (button (make-instance 'button :label "Do"))
           (bold-btn (make-instance 'button :label "Bold"))
           (buffer (make-instance 'text-buffer :text "Some text buffer with some text inside"))
           (v (make-instance 'text-view :buffer buffer :wrap-mode :word))
           (box (make-instance 'v-box))
           (scrolled (make-instance 'scrolled-window :hscrollbar-policy :automatic :vscrollbar-policy :automatic)))
    (g-signal-connect window "destroy" (lambda (w) (release w) (gtk-main-quit)))
    (g-signal-connect button "clicked" (lambda (b)
                                         (release b)
                                         (using* ((i1 (make-instance 'text-iter))
                                                  (i2 (make-instance 'text-iter)))
                                           (multiple-value-bind (i1 i2) (text-buffer-get-selection-bounds buffer)
                                             (when (and i1 i2)
                                               (using* ((i1 i1) (i2 i2)
                                                        (dialog (make-instance 'message-dialog :buttons :ok)))
                                                 (setf (message-dialog-text dialog) (format nil "selection: from (~A,~A) to (~A,~A)"
                                                                                            (text-iter-line i1) (text-iter-line-offset i1)
                                                                                            (text-iter-line i2) (text-iter-line-offset i2)))
                                                 (dialog-run dialog)
                                                 (object-destroy dialog)))))))
    (g-signal-connect bold-btn "clicked" (Lambda (b)
                                           (release b)
                                           (multiple-value-bind (start end) (text-buffer-get-selection-bounds buffer)
                                             (when (and start end)
                                               (using* ((start start) (end end) (tag (text-tag-table-lookup (text-buffer-tag-table buffer) "bold")))
                                                 (if (text-iter-has-tag start tag)
                                                     (text-buffer-remove-tag buffer tag start end)
                                                     (text-buffer-apply-tag buffer tag start end)))))))
    (let ((tag (make-instance 'text-tag :name "bold" :weight 700)))
      (text-tag-table-add (text-buffer-tag-table buffer) tag)
      (g-signal-connect tag "event"
                        (lambda (tag object event iter)
                          (declare (ignore tag object iter))
                          (when (eq (event-type event) :button-release)
                            (using (dlg (make-instance 'message-dialog :text "You clicked on bold text." :buttons :ok))
                              (dialog-run dlg)
                              (object-destroy dlg))))))
    (container-add window box)
    (container-add scrolled v)
    (box-pack-start box button :expand nil)
    (box-pack-start box bold-btn :expand nil)
    (box-pack-start box scrolled)
    (gtk-widget-show-all window)
    (gtk-main)))

(defun demo-code-editor ()
  (using* ((window (make-instance 'gtk-window :type :toplevel :title "Code editor" :width-request 400 :height-request 400 :window-position :center))
           (scrolled (make-instance 'scrolled-window :hscrollbar-policy :automatic :vscrollbar-policy :automatic))
           (buffer (make-instance 'text-buffer))
           (view (make-instance 'text-view :buffer buffer)))
    (g-signal-connect window "destroy" (lambda (w) (release w) (gtk-main-quit)))
    (container-add window scrolled)
    (container-add scrolled view)
    (gtk-widget-show-all window)
    (g-signal-connect buffer "insert-text" (lambda (buffer location text len)
                                             (using* ((buffer buffer) (location location))
                                               (format t "~A~%" (list buffer location text len)))))
    (gtk-main)))

(defstruct tvi title value)

(defun test-treeview-list ()
  (let* ((window (make-instance 'gtk-window :type :toplevel :title "Treeview (list)"))
         (model (make-instance 'array-list-store))
         (scroll (make-instance 'scrolled-window :hscrollbar-policy :automatic :vscrollbar-policy :automatic))
         (tv (make-instance 'tree-view :headers-visible t :width-request 100 :height-request 400 :rules-hint t))
         (h-box (make-instance 'h-box))
         (v-box (make-instance 'v-box))
         (title-entry (make-instance 'entry))
         (value-entry (make-instance 'entry))
         (button (make-instance 'button :label "Add")))
    (store-add-column model "gchararray" #'tvi-title)
    (store-add-column model "gint" #'tvi-value)
    (store-add-item model (make-tvi :title "Monday" :value 1))
    (store-add-item model (make-tvi :title "Tuesday" :value 2))
    (store-add-item model (make-tvi :title "Wednesday" :value 3))
    (store-add-item model (make-tvi :title "Thursday" :value 4))
    (store-add-item model (make-tvi :title "Friday" :value 5))
    (store-add-item model (make-tvi :title "Saturday" :value 6))
    (store-add-item model (make-tvi :title "Sunday" :value 7))
    (setf (tree-view-model tv) model (tree-view-tooltip-column tv) 0)
    (gobject:g-signal-connect window "destroy" (lambda (w) (gobject:release w) (gtk-main-quit)))
    (gobject:g-signal-connect button "clicked" (lambda (b) (gobject:release b) 
                                                       (store-add-item model (make-tvi :title (entry-text title-entry)
                                                                                       :value (or (parse-integer (entry-text value-entry) 
                                                                                                                 :junk-allowed t)
                                                                                                  0)))))
    (g-signal-connect tv "row-activated" (lambda (tv path column)
                                           (release* tv path column)
                                           (format t "You clicked on row ~A~%" (tree-path-indices path))))
    (container-add window v-box)
    (box-pack-start v-box h-box :expand nil)
    (box-pack-start h-box title-entry :expand nil)
    (box-pack-start h-box value-entry :expand nil)
    (box-pack-start h-box button :expand nil)
    (box-pack-start v-box scroll)
    (container-add scroll tv)
    (let ((column (make-instance 'tree-view-column :title "Title" :sort-column-id 0))
          (renderer (make-instance 'cell-renderer-text :text "A text")))
      (tree-view-column-pack-start column renderer)
      (tree-view-column-add-attribute column renderer "text" 0)
      (tree-view-append-column tv column)
      (print (tree-view-column-tree-view column))
      (print (tree-view-column-cell-renderers column)))
    (let ((column (make-instance 'tree-view-column :title "Value"))
          (renderer (make-instance 'cell-renderer-text :text "A text")))
      (tree-view-column-pack-start column renderer)
      (tree-view-column-add-attribute column renderer "text" 1)
      (tree-view-append-column tv column)
      (print (tree-view-column-tree-view column))
      (print (tree-view-column-cell-renderers column)))
    (gtk-widget-show-all window)
    (gtk-main)))

(defun test-toolbar ()
  (let* ((window (make-instance 'gtk-window :type :toplevel :title "Toolbar" :width-request 200 :height-request 100 :window-position :center))
         (v-box (make-instance 'v-box))
         (toolbar (make-instance 'toolbar :toolbar-style :icons :icon-size :small-toolbar :icon-size-set t))
         (l (make-instance 'label :label "Toolbar demo")))
    (gobject:g-signal-connect window "destroy" (lambda (w) (gobject:release w) (gtk-main-quit)))
    (container-add window v-box)
    (box-pack-start v-box toolbar :expand nil)
    (box-pack-start v-box l)
    (let ((b (make-instance 'tool-button :stock-id "gtk-connect")))
      (g-signal-connect b "clicked" (lambda (b)
                                      (setf (tool-button-stock-id b)
                                            (if (string= (tool-button-stock-id b) "gtk-connect")
                                                (prog1 "gtk-disconnect" (setf (label-label l) "Disconnected"))
                                                (prog1 "gtk-connect" (setf (label-label l) "Connected"))))))
      (toolbar-insert toolbar b -1))
    (toolbar-insert toolbar (make-instance 'separator-tool-item) -1)
    (toolbar-insert toolbar (make-instance 'tool-button :stock-id "gtk-undo" :sensitive nil) -1)
    (toolbar-insert toolbar (make-instance 'tool-button :stock-id "gtk-redo") -1)
    (gtk-widget-show-all window)
    (gtk-main)))

(defun test-color-button ()
  (let* ((window (make-instance 'gtk-window :type :toplevel :title "Color button" :width-request 200 :height-request 100 :window-position :center))
         (button (make-instance 'color-button :label "Choose your color" :use-alpha t)))
    (gobject:g-signal-connect window "destroy" (lambda (w) (gobject:release w) (gtk-main-quit)))
    (container-add window button)
    (setf (color-button-color button)
          (make-color :red (random 65536) :green (random 65536) :blue (random 65536)))
    (gtk-widget-show-all window)
    (gtk-main)))

(defun test-ui-manager ()
  (let* ((window (make-instance 'gtk-window :type :toplevel :title "UI Manager" :default-width 200 :default-height 100 :window-position :center))
         (ui-manager (make-instance 'ui-manager))
         (print-confirmation t))
    (ui-manager-add-ui-from-string ui-manager
                                   "
<ui>
  <toolbar action='toolbar1'>
      <separator/>
      <toolitem name='Left' action='justify-left'/>
      <toolitem name='Center' action='justify-center'/>
      <toolitem name='Right' action='justify-right'/>
      <toolitem name='Zoom in' action='zoom-in' />
      <toolitem name='print-confirm' action='print-confirm' />
      <separator/>
  </toolbar>
</ui>")
    (gobject:g-signal-connect window "destroy" (lambda (w) (gobject:release w) (gtk-main-quit)))
    (iter (with fn = (lambda (action) (when print-confirmation (format t "Action ~A with name ~A activated~%" action (action-name action)))))
          (with action-group = (make-instance 'action-group :name "Actions"))
          (finally (let ((a (make-instance 'toggle-action :name "print-confirm" :label "Print" :stock-id "gtk-print-report" :active t)))
                     (g-signal-connect a "toggled" (lambda (action) (setf print-confirmation (toggle-action-active action))))
                     (action-group-add-action action-group a))
                   (ui-manager-insert-action-group ui-manager action-group 0))
          (for (name stock-id) in '(("justify-left" "gtk-justify-left")
                                    ("justify-center" "gtk-justify-center")
                                    ("justify-right" "gtk-justify-right")
                                    ("zoom-in" "gtk-zoom-in")))
          (for action = (make-instance 'action :name name :stock-id stock-id))
          (g-signal-connect action "activate" fn)
          (action-group-add-action action-group action))
    (awhen (ui-manager-widget ui-manager "/toolbar1")
      (container-add window it))
    (gtk-widget-show-all window)
    (gtk-main)))